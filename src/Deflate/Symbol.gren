module Deflate.Symbol exposing (Symbol(..), buildBitWidthCodes, buildDynamicHuffmanCodec, buildFixedHuffmanCodec, calculateCodes, calculateRunLengths, code, dynamicFindFrequencies, encode, writeDynamicHuffmanCodec)

import Deflate.BitWriter as BitWriter exposing (BitWriter)
import Huffman as Huffman
import Math


bitwidth_code_order : Array Int
bitwidth_code_order =
    [ 16, 17, 18, 0, 8, 7, 9, 6, 10, 5, 11, 4, 12, 3, 13, 2, 14, 1, 15 ]


type Symbol
    = EndOfBlock
    | Literal Int
    | Share Int Int


code : Symbol -> Int
code symbol =
    case symbol of
        Literal byte ->
            byte

        EndOfBlock ->
            256

        Share length _ ->
            if length >= 3 && length <= 10 then
                257 + length - 3

            else if length >= 11 && length <= 18 then
                265 + (length - 11) // 2

            else if length >= 19 && length <= 34 then
                269 + (length - 19) // 4

            else if length >= 35 && length <= 66 then
                273 + (length - 35) // 8

            else if length >= 67 && length <= 130 then
                277 + (length - 67) // 16

            else if length >= 131 && length <= 257 then
                281 + (length - 131) // 32

            else if length == 258 then
                285

            else
                -- unreachable
                -1


extraLength : Symbol -> Maybe { v1 : Int, v2 : Int }
extraLength symbol =
    case symbol of
        Share length _ ->
            if (length >= 3 && length <= 10) || length == 258 then
                Nothing

            else if length >= 11 && length <= 18 then
                Just { v1 = 1, v2 = (length - 11) |> Math.modBy 2 }

            else if length >= 19 && length <= 34 then
                Just { v1 = 2, v2 = (length - 19) |> Math.modBy 4 }

            else if length >= 35 && length <= 66 then
                Just { v1 = 3, v2 = (length - 35) |> Math.modBy 8 }

            else if length >= 67 && length <= 130 then
                Just { v1 = 4, v2 = (length - 67) |> Math.modBy 16 }

            else if length >= 131 && length <= 257 then
                Just { v1 = 5, v2 = (length - 131) |> Math.modBy 32 }

            else
                -- unreachable
                Nothing

        _ ->
            Nothing


distance : Symbol -> Maybe { v1 : Int, v2 : Int, v3 : Int }
distance symbol =
    case symbol of
        Share _ distance_ ->
            if distance_ <= 4 then
                Just { v1 = distance_ - 1, v2 = 0, v3 = 0 }

            else
                let
                    go extraBits code_ base =
                        if base * 2 < distance_ then
                            go (extraBits + 1) (code_ + 2) (base * 2)

                        else
                            { v1 = extraBits, v2 = code_, v3 = base }
                in
                let
                    { v1 = extraBits, v2 = code_, v3 = base } =
                        go 1 4 4

                    half =
                        base // 2

                    delta =
                        distance_ - base - 1
                in
                if distance_ <= base + half then
                    Just { v1 = code_, v2 = extraBits, v3 = delta |> Math.modBy half }

                else
                    Just { v1 = code_ + 1, v2 = extraBits, v3 = delta |> Math.modBy half }

        _ ->
            Nothing


encode : Symbol -> { literal : Huffman.Tree, distance : Huffman.Tree } -> BitWriter -> BitWriter
encode symbol htrees bitWriter =
    let
        maybeExtra =
            case extraLength symbol of
                Nothing ->
                    identity

                Just { v1 = bits, v2 = extra } ->
                    BitWriter.writeBits bits extra

        maybeDistance =
            case distance symbol of
                Nothing ->
                    identity

                Just { v1 = code_, v2 = bits, v3 = extra } ->
                    Huffman.encode code_ htrees.distance
                        >> (if bits > 0 then
                                BitWriter.writeBits bits extra

                            else
                                identity
                           )
    in
    bitWriter
        |> Huffman.encode (code symbol) htrees.literal
        |> maybeExtra
        |> maybeDistance



-- huffman codecs


type Range
    = Range Int Int


fixed_literal_or_length_code_table =
    [ { v1 = 8, v2 = Range 0 144, v3 = 48 }
    , { v1 = 9, v2 = Range 144 256, v3 = 400 }
    , { v1 = 7, v2 = Range 256 280, v3 = 0 }
    , { v1 = 8, v2 = Range 280 288, v3 = 192 }
    ]


buildFixedHuffmanCodec : { literal : Huffman.Tree, distance : Huffman.Tree }
buildFixedHuffmanCodec =
    let
        innerFolder bitwidth { v1 = code_, v2 = symbol } currentTree =
            Huffman.setMapping symbol (Huffman.newCode bitwidth code_) currentTree

        folder { v1 = bitwidth, v2 = (Range start end) as symbols, v3 = codeBase } huffmanTree =
            let
                domain =
                    Array.indexedMap (\i s -> { v1 = codeBase + 1, v2 = s }) (Array.range start end)
            in
            Array.foldl (innerFolder bitwidth) huffmanTree domain

        literal =
            Array.foldl folder (Huffman.new 288) fixed_literal_or_length_code_table

        distance_ =
            Array.range 0 (30 - 1)
                |> Array.foldl (\i huffmanTree -> Huffman.setMapping i (Huffman.newCode 5 i) huffmanTree) (Huffman.new 30)
    in
    { literal = literal, distance = distance_ }


buildDynamicHuffmanCodec : Array Symbol -> { literal : Huffman.Tree, distance : Huffman.Tree }
buildDynamicHuffmanCodec symbols =
    let
        { v1 = literalCounts, v2 = distanceCounts, v3 = emptyDistanceCount } =
            Array.foldl dynamicFindFrequencies { v1 = Array.repeat 286 0, v2 = Array.repeat 30 0, v3 = True } symbols
    in
    { literal = Huffman.fromFrequencies literalCounts 15
    , distance =
        if emptyDistanceCount then
            Huffman.fromFrequencies (Array.set 0 1 distanceCounts) 15

        else
            Huffman.fromFrequencies distanceCounts 15
    }


dynamicFindFrequencies : Symbol -> { v1 : Array Int, v2 : Array Int, v3 : Bool } -> { v1 : Array Int, v2 : Array Int, v3 : Bool }
dynamicFindFrequencies symbol { v1 = literalCounts, v2 = distanceCounts, v3 = emptyDistanceCount } =
    case distance symbol of
        Nothing ->
            { v1 = update (code symbol) (\v -> v + 1) literalCounts, v2 = distanceCounts, v3 = emptyDistanceCount }

        Just { v1 = d, v2 = _, v3 = _ } ->
            { v1 = update (code symbol) (\v -> v + 1) literalCounts
            , v2 = update d (\v -> v + 1) distanceCounts
            , v3 = False
            }


update index tagger array =
    case Array.get index array of
        Nothing ->
            array

        Just value ->
            Array.set index (tagger value) array


writeDynamicHuffmanCodec : { literal : Huffman.Tree, distance : Huffman.Tree } -> BitWriter -> BitWriter
writeDynamicHuffmanCodec trees bitWriter =
    let
        literal_code_count =
            max 257 ((Huffman.usedMaxSymbol trees.literal |> Maybe.withDefault 0) + 1)

        distance_code_count =
            max 1 ((Huffman.usedMaxSymbol trees.distance |> Maybe.withDefault 0) + 1)

        codes =
            buildBitWidthCodes literal_code_count distance_code_count { literal = trees.literal, distance = trees.distance }

        codeCounts =
            Array.foldl (\{ v1 = i, v2 = _, v3 = _ } -> update i (\v -> v + 1)) (Array.repeat 19 0) codes

        bitWidthEncoder =
            Huffman.fromFrequencies codeCounts 7

        bitwidthCodeCount =
            max 4
                (bitwidth_code_order
                    |> Array.reverse
                    |> position
                        (\i ->
                            case Huffman.lookup i bitWidthEncoder of
                                Nothing ->
                                    False

                                Just value ->
                                    Huffman.getWidth value > 0
                        )
                    |> Maybe.map (\trailingZeros -> 19 - trailingZeros)
                    |> Maybe.withDefault 0
                )

        v1 : BitWriter -> BitWriter
        v1 writer =
            Array.takeFirst bitwidthCodeCount bitwidth_code_order
                |> Array.foldl
                    (\i current ->
                        let
                            width =
                                if Array.get i codeCounts == Just 0 then
                                    0

                                else
                                    Huffman.lookup i bitWidthEncoder
                                        |> Maybe.map Huffman.getWidth
                                        |> Maybe.withDefault 0
                        in
                        current
                            |> BitWriter.writeBits 3 width
                    )
                    writer

        v2 : BitWriter -> BitWriter
        v2 writer =
            codes
                |> Array.foldl
                    (\{ v1 = code_, v2 = bits, v3 = extra } current ->
                        if bits > 0 then
                            current
                                |> Huffman.encode code_ bitWidthEncoder
                                |> BitWriter.writeBits bits extra

                        else
                            current
                                |> Huffman.encode code_ bitWidthEncoder
                    )
                    writer
    in
    bitWriter
        |> BitWriter.writeBits 5 (literal_code_count - 257)
        |> BitWriter.writeBits 5 (distance_code_count - 1)
        |> BitWriter.writeBits 4 (bitwidthCodeCount - 4)
        |> v1
        |> v2



-- Build Bitwidth Codes


type alias RunLength =
    { value : Int, count : Int }


buildBitWidthCodes : Int -> Int -> { literal : Huffman.Tree, distance : Huffman.Tree } -> Array { v1 : Int, v2 : Int, v3 : Int }
buildBitWidthCodes literalCodeCount distanceCodeCount trees =
    let
        runLengths =
            calculateRunLengths [ { v1 = trees.literal, v2 = literalCodeCount }, { v1 = trees.distance, v2 = distanceCodeCount } ] []
    in
    calculateCodes runLengths


calculateRunLengths : Array { v1 : Huffman.Tree, v2 : Int } -> Array RunLength -> Array RunLength
calculateRunLengths lengths accum =
    case Array.popFirst lengths of
        Nothing ->
            Array.foldr Array.pushLast Array.empty accum

        Just { first = { v1 = e, v2 = size }, rest = rest } ->
            let
                list =
                    Array.range 0 (size - 1)
                        |> Array.map (\x -> Huffman.lookup x e |> Maybe.map Huffman.getWidth |> Maybe.withDefault 0)
                        |> Array.indexedMap ( \ v1 v2 -> { v1 = v1, v2 = v2 } )

                folder { v1 = i, v2 = c } runLengths =
                    case Array.popFirst runLengths of
                        Nothing ->
                            Array.pushFirst { value = c, count = 1 } runLengths

                        Just { first = last, rest = remaining } ->
                            if last.value == c then
                                Array.pushFirst { value = last.value, count = last.count + 1 } remaining

                            else
                                Array.pushFirst { value = c, count = 1 } runLengths
            in
            calculateRunLengths rest (Array.foldl folder accum list)


position : (a -> Bool) -> Array a -> Maybe Int
position predicate elements =
    positionLoop predicate 0 elements


positionLoop predicate i elements =
    case Array.popFirst elements of
        Nothing ->
            Nothing

        Just { first = x, rest = xs } ->
            if predicate x then
                Just i

            else
                positionLoop predicate (i + 1) xs


calculateCodes : Array RunLength -> Array { v1 : Int, v2 : Int, v3 : Int }
calculateCodes runLengths =
    let
        loop1 c codes =
            if c >= 11 then
                let
                    n =
                        min 138 c
                in
                loop1 (c - n) (Array.pushLast { v1 = 18, v2 = 7, v3 = n - 11 } codes)

            else if c >= 3 then
                Array.pushLast { v1 = 17, v2 = 3, v3 = c - 3 } codes

            else
                Array.append codes (Array.repeat c { v1 = 0, v2 = 0, v3 = 0 })

        loop2 r c codes =
            if c >= 3 then
                let
                    n =
                        min 6 c
                in
                loop2 r (c - n) (Array.pushLast { v1 = 16, v2 = 2, v3 = n - 3 } codes)

            else
                Array.append codes (Array.repeat c { v1 = r.value, v2 = 0, v3 = 0 })

        folder r codes =
            if r.value == 0 then
                loop1 r.count codes

            else
                loop2 r (r.count - 1) (Array.pushLast { v1 = r.value, v2 = 0, v3 = 0 } codes)
    in
    Array.foldl folder Array.empty runLengths
